@DSL DefaultModelDSL{
  automatic_declaration_of_the_temperature_as_first_external_state_variable :
      false
};
@Model DuctileDamageIndicator_CockcroftLatham1968;

@AuxiliaryStateVariable real di;
di.setEntryName("DamageIndicator");
@ExternalStateVariable strain p;
p.setGlossaryName("EquivalentPlasticStrain");
@ExternalStateVariable StressStensor sig;
sig.setGlossaryName("Stress");

@MaterialProperty real D1;

@Integrator {
  if (dp > 0){
    // stress at the middle of the time step (>0 as dp>0)
    const auto sig_mts = eval(sig + dsig / 2);
    // compute eigenvalues
    const auto svp_mts = sig_mts.computeEigenValues();
    // sort eigenvalues in descending orger
    const auto [s1_mts, s2_mts, s3_mts] = sortEigenValues(svp_mts);
    // compute critical strain
    const auto ec_mts = D1 / s1_mts;
    const auto ec_mts_pos = ec_mts > 0 ? ec_mts : 1.e12;
    // update damage indicator
    di += dp / ec_mts_pos;
  }
}
