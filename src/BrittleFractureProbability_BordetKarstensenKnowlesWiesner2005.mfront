@DSL DefaultModelDSL{
  automatic_declaration_of_the_temperature_as_first_external_state_variable :
      false
};
@Model BrittleFractureProbability_BordetKarstensenKnowlesWiesner2005;

@AuxiliaryStateVariable real pf;
pf.setEntryName("LocalFractureProbability");
@ExternalStateVariable strain p;
p.setGlossaryName("EquivalentPlasticStrain");
@ExternalStateVariable StressStensor sig;
sig.setGlossaryName("Stress");

@MaterialProperty real sigma_u;
@MaterialProperty real m;
@MaterialProperty real sigma_th;
@MaterialProperty real alpha_p;
@MaterialProperty real beta_p;


@Integrator {
  if (dp > 0){
    // stress at the middle of timestep
    const auto sig_mts = eval(sig + dsig / 2);
    // compute eigenvalues
    const auto svp_mts = sig_mts.computeEigenValues();
    // sort eigenvalues in descending order
    const auto [s1_mts, s2_mts, s3_mts] = sortEigenValues(svp_mts);
    if (s1_mts > sigma_th){
      const auto sI_su = s1_mts / sigma_u;
      const auto sth_su = sigma_th / sigma_u;
      // compute the increase in local fracture probability
      const auto dpf = alpha_p * exp(-beta_p * (p + dp / 2)) * (pow(sI_su,m) - pow(sth_su,m)) * dp;
      // compute the new local fracture probability
      pf = pf + dpf;
    }
  }
}
