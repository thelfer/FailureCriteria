@DSL DefaultModelDSL{
  automatic_declaration_of_the_temperature_as_first_external_state_variable :
      false
};
@Model BrittleFractureProbability_BakkerKoers1991;

@AuxiliaryStateVariable real pf;
pf.setEntryName("LocalFractureProbability");
@ExternalStateVariable strain p;
p.setGlossaryName("EquivalentPlasticStrain");
@ExternalStateVariable StressStensor sig;
sig.setGlossaryName("Stress");
@ExternalStateVariable StrainStensor eto;
eto.setGlossaryName("Strain");

@MaterialProperty real sigma_u;
@MaterialProperty real m;
@MaterialProperty real sigma_th;

@Integrator {
  if (dp > 0){
    // stress at the end of timestep
    const auto sig_ets = eval(sig + dsig);
    // compute eigenvalues
    const auto svp_ets = sig_ets.computeEigenValues();
    // sort eigenvalues in descending order
    const auto [s1_ets, s2_ets, s3_ets] = sortEigenValues(svp_ets);
    if (s1_ets > sigma_th){
      // strain at the end of timestep
      const auto eto_ets = eval(eto + deto);
      // compute eigenvalues
      const auto evp_ets = eto_ets.computeEigenValues();
      // sort eigenvalues in descending order
      const auto [e1_ets, e2_ets, e3_ets] = sortEigenValues(evp_ets);
      // compute the tentative local fracture probability
      const auto pnew = pow((s1_ets - sigma_th)* exp(-e1_ets/2) / sigma_u,m);
      // compute the new local fracture probability
      pf = max(pf, pnew);
    }
  }
}
