@DSL DefaultModelDSL{
  automatic_declaration_of_the_temperature_as_first_external_state_variable :
      false
};
@Model BrittleFractureProbability_RuggieriDodds2015;

@AuxiliaryStateVariable real pf;
pf.setEntryName("LocalFractureProbability");
@ExternalStateVariable strain p;
p.setGlossaryName("EquivalentPlasticStrain");
@ExternalStateVariable StressStensor sig;
sig.setGlossaryName("Stress");

@MaterialProperty real sigma_u;
@MaterialProperty real m;
@MaterialProperty real alpha_p;

@Integrator {
  // stress at the end of timestep
  const auto sig_ets = eval(sig + dsig);
  // compute eigenvalues
  const auto svp_ets = sig_ets.computeEigenValues();
  // sort eigenvalues in descending order
  const auto [s1_ets, s2_ets, s3_ets] = sortEigenValues(svp_ets);
  if (s1_ets > 0){
    // compute the tentative local fracture probability
    const auto pnew = (1 - exp(-alpha_p * (p + dp))) * pow(s1_ets / sigma_u,m);
    // compute the new local fracture probability
    pf = max(pf, pnew);
  }
}
