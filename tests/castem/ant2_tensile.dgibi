MESH = 'ant2_10';

* Options

OPTI DIME 2 ELEM QUA8 MODE AXIS;

* Mesh

ME = LIRE 'MED' (CHAI '../mesh/' MESH '.med');
ZMAX = MAXI (COOR 2 ME.ST);
ZMIN = MINI (COOR 2 ME.ST);
LB = ME.ST POIN DROI (0. ZMIN) (1. ZMIN) 0.000001;
LH = ME.ST POIN DROI (0. ZMAX) (1. ZMAX) 0.000001;
LM = ME.ST POIN DROI (0. 0.) (1. 0.) 0.000001;
PEXT = LM POIN PROC (10. 0.);

* Model

STATEV1 = 'MOTS' 'EERR' 'EEZZ' 'EETT' 'EERZ';
STATEV2 = 'MOTS' 'P';
STATEV3 = 'MOTS' 'ETRR' 'ETZZ' 'ETTT' 'ETRZ';
STATEV = STATEV1 ET STATEV2 ET STATEV3;

COEF = 'MOTS' 'YOUN' 'NU' 'RHO' 'ALPH' 'TALP' 'TREF';
MOD1 = 'MODELISER' (ME.ST) 'MECANIQUE' 'ELASTIQUE' 'NON_LINEAIRE'
'UTILISATEUR' 'Q8RI' 'LIB_LOI' './src/libUmatBehaviour.so' 'FCT_LOI'
'umatldc_malls' 'C_MATERIAU' COEF 'C_VARINTER' STATEV;
MAT1 = 'MATERIAU' MOD1 'YOUN' 200000. 'NU' 0.3 'RHO' 0. 'ALPH' 0. 'TALP' 0. 'TREF' 0.;

* Loading 

NPAS = 100;
CL1 = 'BLOQUE' LB UZ;
CL2 = 'BLOQUE' LH UZ;

CHA0 = DEPI CL2 0.64;
LT1 = PROG 0. PAS (1./NPAS) 1.;
EV1 = EVOL MANU LT1 LT1;
CHA1 = CHAR DIMP CHA0 EV1;

* Computation

TAB1 = TABLE;
TAB1.'MODELE' = MOD1;
TAB1.'CARACTERISTIQUES' = MAT1;
TAB1.'BLOCAGES_MECANIQUES' = CL1 'ET' CL2;
TAB1.'CHARGEMENT' = CHA1;
TAB1.'TEMPS_CALCULES' = LT1;
TAB1.'TEMPS_SAUVES' = LT1;
TAB1.'HYPOTHESE_DEFORMATIONS' = 'MOT' 'UTILISATEUR';
TAB1.'GRANDS_DEPLACEMENTS' = VRAI;
TAB1.'LAGRANGIEN' = 'MOT' 'REACTUALISE';
TAB1.'MOVA' = 'MOT' 'RIEN' ;
TAB1.'PROCESSEURS' = 'MOT' 'COMPORTEMENT';

PASAPAS TAB1;

* Post

FORC = PROG 0.;
DEP1 = PROG 0.;
DEP2 = PROG 0.;

UN = MANU CHPO LH 1 'SCAL' 1.;
UNV = MANU CHML MOD1 'VOLU' 1. 'STRESSES';
SIGCOMP = 'MOTS' 'SMRR' 'SMZZ' 'SMTT' 'SMRZ';
DEPCOMP = 'MOTS' 'UX' 'UY' 'UZ';

REPE BO1 NPAS;

CONF1 = FORM;
CONF2 = FORM TAB1.DEPLACEMENTS.&BO1;
REPE BO2 (NBEL ME.ST);
REPE BO3 4;
TMP1 = MANU CHAM MOD1 'POSI' 'STRESSES' 'VOLU' &BO2 &BO3 1.;
TMP2 = INTG MOD1 TMP1;
SI ((&BO2 EGA 1) ET (&BO3 EGA 1));
VOLU = (TMP1 * TMP2);
SINON;
VOLU = VOLU '+' (TMP1 * TMP2);
FINSI;
FIN BO3;
FIN BO2;
FORM CONF1;

DEPE = TAB1.DEPLACEMENTS.&BO1;
DEPE = (NOMC 'UX' (EXCO DEPE 'UR')) ET 
       (NOMC 'UY' (EXCO DEPE 'UZ')) ET
       (NOMC 'UZ' (0*(EXCO DEPE 'UR')));

DEPP = CHAN CHAM DEPE MOD1 STRESSES;
TMP1 = DEPP ET VOLU ET TAB1.CONTRAINTES.&BO1 ET TAB1.VARIABLES_INTERNES.&BO1;
TMP2 = CHAN CHPO MOD1 TMP1 SUPP;
PG = EXTR TMP2 MAILL;

TMP3A = CHAN GRAVITE MOD1 (TAB1.CONTRAINTES.&BO1 ET TAB1.VARIABLES_INTERNES.&BO1);
TMP3B = NOMC (INTG ELEM UNV MOD1) 'VOLU';
TMP3 = TMP3A ET TMP3B;
MG = EXTR TMP3 MAILL;

OPTI SORT (CHAI '../results/ant2_tensile/' MESH '_tensile_gauss_points');
SORT VTK PG 'MESH' 
 (EXCO TMP2 DEPCOMP) 'DEPL'
 (EXCO TMP2 SIGCOMP) 'SIG' 
 (EXCO TMP2 STATEV2) 'P'
 (EXCO TMP2 STATEV3) 'ETO'
 (EXCO TMP2 'VOLU') 'VOLU'
TEMPS &BO1 FORMAT;

OPTI SORT (CHAI '../results/ant2_tensile/' MESH '_tensile_elements');
SORT VTK MG 'MESH' 
 DEPE 'DEPL'
 (EXCO TMP3 SIGCOMP) 'SIG'
 (EXCO TMP3 STATEV2) 'P'
 (EXCO TMP3 STATEV3) 'ETO'
 (EXCO TMP3 'VOLU') 'VOLU'
TEMPS &BO1 FORMAT;

TMP1 = XTY UN (EXCO TAB1.REACTIONS.&BO1 FZ) ('MOTS' 'SCAL') ('MOTS' 'SCAL');
FORC = FORC ET (PROG TMP1);

TMP1 = EXTR TAB1.DEPLACEMENTS.&BO1 UZ (POIN 1 LH);
DEP1 = DEP1 ET (PROG TMP1);

TMP1 = EXTR TAB1.DEPLACEMENTS.&BO1 UR PEXT;
DEP2 = DEP2 ET (PROG TMP1);

OPTI SORT (CHAI '../results/ant2_tensile/' MESH '_tensile.csv');
SORT EXCE FORC DEP1 DEP2 SEPA TABU;

FIN BO1;

FIN;

